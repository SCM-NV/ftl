! The algorithms library defines functions for a variety of purposes (e.g. searching, sorting, counting, manipulating)
! that operate on ranges of elements. Note that a range is defined as [first, last) where last refers to the element past
! the last element to inspect or modify.
!
! A range is any sequence of objects that can be accessed through iterators or pointers, such as an array or an instance
! of some of the FTL containers. Notice though, that algorithms operate through iterators directly on the values, not
! affecting in any way the structure of any possible container (it never affects the size or storage allocation of the
! container).

#ifdef FTL_INSTANTIATE_TEMPLATE

#if !defined(FTL_TEMPLATE_TYPE)
#error FTL_TEMPLATE_TYPE must be defined when instantiating ftlAlgorithms
#endif
#if !defined(FTL_TEMPLATE_TYPE_NAME)
#error FTL_TEMPLATE_TYPE_NAME must be defined when instantiating ftlAlgorithms
#endif
#if !defined(FTL_CONTAINER)
#error FTL_CONTAINER must be defined when instantiating ftlAlgorithms
#endif
#if !defined(FTL_CONTAINER_PROVIDES_FORWARD_ITERATOR)       && \
    !defined(FTL_CONTAINER_PROVIDES_BIDIRECTIONAL_ITERATOR) && \
    !defined(FTL_CONTAINER_PROVIDES_RANDOM_ACCESS_ITERATOR)
#error FTL_CONTAINER_PROVIDES_*_INTERATOR must be defined when instantiating ftlAlgorithms
#endif

#include "ftlMacros.inc"


module CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,AlgorithmsModule)

#ifdef FTL_TEMPLATE_TYPE_MODULE
   use FTL_TEMPLATE_TYPE_MODULE
#endif
   use CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Module)

   implicit none
   private


   ! =============> Non-modifying sequence operations:

   public :: ftlAllOf
   interface ftlAllOf
      module procedure ftlAllOfIteratorPair
      module procedure ftlAllOfContainer
   end interface

   public :: ftlAnyOf
   interface ftlAnyOf
      module procedure ftlAnyOfIteratorPair
      module procedure ftlAnyOfContainer
   end interface

   public :: ftlNoneOf
   interface ftlNoneOf
      module procedure ftlNoneOfIteratorPair
      module procedure ftlNoneOfContainer
   end interface

   public :: ftlForEach
   interface ftlForEach
      module procedure ftlForEachIteratorPair
      module procedure ftlForEachContainer
   end interface

   public :: ftlFind
   interface ftlFind
      module procedure ftlFindIteratorPair
      module procedure ftlFindContainer
   end interface

   public :: ftlFindIf
   interface ftlFindIf
      module procedure ftlFindIfIteratorPair
      module procedure ftlFindIfContainer
   end interface

   public :: ftlFindIfNot
   interface ftlFindIfNot
      module procedure ftlFindIfNotIteratorPair
      module procedure ftlFindIfNotContainer
   end interface

   ! TODO: ftlFindEnd

   public :: ftlFirstOf
   interface ftlFirstOf
      module procedure ftlFirstOfIteratorPair
      module procedure ftlFirstOfContainer
   end interface

   ! TODO: ftlAdjacentFind

   public :: ftlCount
   interface ftlCount
      module procedure ftlCountIteratorPair
      module procedure ftlCountContainer
   end interface

   public :: ftlCountIf
   interface ftlCountIf
      module procedure ftlCountIfIteratorPair
      module procedure ftlCountIfContainer
   end interface

   public :: ftlMismatch
   interface ftlMismatch
      module procedure ftlMismatchIterators
      module procedure ftlMismatchContainers
   end interface

   public :: ftlEqual
   interface ftlEqual
      module procedure ftlEqualIterators
      module procedure ftlEqualContainers
   end interface

   public :: ftlIsPermutation
   interface ftlIsPermutation
      module procedure ftlIsPermutationIterators
      module procedure ftlIsPermutationContainers
   end interface

   public :: ftlSearch
   interface ftlSearch
      module procedure ftlSearchIterators
      module procedure ftlSearchContainers
   end interface

   ! TODO: ftlSearchN


   ! =============> Modifying sequence operations:

   public :: ftlIterSwap
   interface ftlIterSwap
      module procedure ftlIterSwapPrivate
   end interface


   ! =============> Partitions:

   public :: ftlIsPartitioned
   interface ftlIsPartitioned
      module procedure ftlIsPartitionedIterators
      module procedure ftlIsPartitionedContainer
   end interface

   public :: ftlPartition
   interface ftlPartition
      module procedure ftlPartitionIterators
      module procedure ftlPartitionContainer
   end interface


   ! =============> Sorting operations:

   public :: ftlSort
   interface ftlSort
      module procedure ftlSortContainer
   end interface

   public :: ftlIsSorted
   interface ftlIsSorted
      module procedure ftlIsSortedIterators
      module procedure ftlIsSortedContainer
   end interface

   public :: ftlIsSortedUntil
   interface ftlIsSortedUntil
      module procedure ftlIsSortedUntilIterators
      module procedure ftlIsSortedUntilContainer
   end interface


   ! =============> Iterator operations:
   ! (in C++ these are in the <iterator> header, not in <algorithm>)

   public :: ftlIterCompare ! doesn't exist in C++ but is incredibly useful ...
   interface ftlIterCompare
      module procedure ftlIterComparePrivate
   end interface

   public :: ftlAdvance
   interface ftlAdvance
      module procedure ftlAdvancePrivate
   end interface

   public :: ftlDistance
   interface ftlDistance
      module procedure ftlDistancePrivate
   end interface


#ifdef FTL_CONTAINER_PROVIDES_RANDOM_ACCESS_ITERATOR
   ! =============> Heap:

   public :: ftlMakeHeap
   interface ftlMakeHeap
      module procedure ftlMakeHeapIterators
      module procedure ftlMakeHeapContainer
   end interface

   public :: ftlPushHeap
   interface ftlPushHeap
      module procedure ftlPushHeapIterators
      module procedure ftlPushHeapContainer
   end interface

   public :: ftlPopHeap
   interface ftlPopHeap
      module procedure ftlPopHeapIterators
      module procedure ftlPopHeapContainer
   end interface

   public :: ftlIsHeap
   interface ftlIsHeap
      module procedure ftlIsHeapIterators
      module procedure ftlIsHeapContainer
   end interface

   public :: ftlIsHeapUntil
   interface ftlIsHeapUntil
      module procedure ftlIsHeapUntilIterators
      module procedure ftlIsHeapUntilContainer
   end interface
#endif


   ! =============> Interfaces for dummy procedures:

   abstract interface

      logical function UnaryPredicate(a)
         FTL_TEMPLATE_TYPE, intent(in) :: a
      end function

      logical function Comparator(a, b)
         FTL_TEMPLATE_TYPE, intent(in) :: a, b
      end function

      subroutine UnarySubroutine(a)
         FTL_TEMPLATE_TYPE, intent(inout) :: a
      end subroutine

   end interface


contains



   ! =============> Non-modifying sequence operations:



   ! Returns true if pred returns true for all the elements in the range [first,last) or if the range is empty,
   ! and false otherwise.
   !
   logical function ftlAllOfIteratorPair(first, last, pred) result(alltrue)
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)), intent(in) :: first, last
      procedure(UnaryPredicate)                                             :: pred

      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)) :: it

      alltrue = .true.
      it = first
      do while (it /= last)
         if (.not.pred(it%value)) then
            alltrue = .false.
            exit
         endif
         call it%Inc()
      enddo

   end function
   !
   logical function ftlAllOfContainer(container, pred) result(alltrue)
      type(CAT(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME)), intent(in) :: container
      procedure(UnaryPredicate)                                   :: pred

      alltrue = ftlAllOfIteratorPair(container%Begin(), container%End(), pred)

   end function



   ! Returns true if pred returns true for any of the elements in the range [first,last), and false otherwise.
   !
   ! If [first,last) is an empty range, the function returns false.
   !
   logical function ftlAnyOfIteratorPair(first, last, pred) result(anytrue)
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)), intent(in) :: first, last
      procedure(UnaryPredicate)                                             :: pred

      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)) :: it

      anytrue = .false.
      it = first
      do while (it /= last)
         if (pred(it%value)) then
            anytrue = .true.
            exit
         endif
         call it%Inc()
      enddo

   end function
   !
   logical function ftlAnyOfContainer(container, pred) result(anytrue)
      type(CAT(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME)), intent(in) :: container
      procedure(UnaryPredicate)                                   :: pred

      anytrue = ftlAnyOfIteratorPair(container%Begin(), container%End(), pred)

   end function



   ! Returns true if pred returns false for all the elements in the range [first,last) or if the range is empty,
   ! and false otherwise.
   !
   logical function ftlNoneOfIteratorPair(first, last, pred) result(nonetrue)
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)), intent(in) :: first, last
      procedure(UnaryPredicate)                                             :: pred

      nonetrue = .not.ftlAnyOfIteratorPair(first, last, pred)

   end function
   !
   logical function ftlNoneOfContainer(container, pred) result(nonetrue)
      type(CAT(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME)), intent(in) :: container
      procedure(UnaryPredicate)                                 :: pred

      nonetrue = .not.ftlAnyOfIteratorPair(container%Begin(), container%End(), pred)

   end function



   ! Calls subroutine subr on each of the elements in the range [first,last).
   !
   subroutine ftlForEachIteratorPair(first, last, subr)
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)), intent(in) :: first, last
      procedure(UnarySubroutine)                                            :: subr

      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)) :: it

      it = first
      do while (it /= last)
         call subr(it%value)
         call it%Inc()
      enddo

   end subroutine
   !
   subroutine ftlForEachContainer(container, subr)
      type(CAT(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME)), intent(in) :: container
      procedure(UnarySubroutine)                                  :: subr

      call ftlForEachIteratorPair(container%Begin(), container%End(), subr)

   end subroutine



   ! Returns an iterator to the first element in the range [first,last) that compares equal to val. If no such element is
   ! found, the function returns last.
   !
   ! The function uses operator== to compare the individual elements to val.
   !
   function ftlFindIteratorPair(first, last, val) result(it)
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)), intent(in) :: first, last
      FTL_TEMPLATE_TYPE                                        , intent(in) :: val
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator))             :: it

      it = first
      do while (it /= last)
         if (it%value == val) exit
         call it%Inc()
      enddo

   end function
   !
   function ftlFindContainer(container, val) result(it)
      type(CAT(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME))          , intent(in) :: container
      FTL_TEMPLATE_TYPE                                        , intent(in) :: val
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator))             :: it

      it = ftlFindIteratorPair(container%Begin(), container%End(), val)

   end function



   ! Returns an iterator to the first element in the range [first,last) for which pred returns true. If no such element is
   ! found, the function returns last.
   !
   function ftlFindIfIteratorPair(first, last, pred) result(it)
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)), intent(in) :: first, last
      procedure(UnaryPredicate)                                             :: pred
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator))             :: it

      it = first
      do while (it /= last)
         if (pred(it%value)) exit
         call it%Inc()
      enddo

   end function
   !
   function ftlFindIfContainer(container, pred) result(it)
      type(CAT(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME))          , intent(in) :: container
      procedure(UnaryPredicate)                                             :: pred
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator))             :: it

      it = ftlFindIfIteratorPair(container%Begin(), container%End(), pred)

   end function



   ! Returns an iterator to the first element in the range [first,last) for which pred returns false. If no such element
   ! is found, the function returns last.
   !
   function ftlFindIfNotIteratorPair(first, last, pred) result(it)
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)), intent(in) :: first, last
      procedure(UnaryPredicate)                                             :: pred
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator))             :: it

      it = first
      do while (it /= last)
         if (.not.pred(it%value)) exit
         call it%Inc()
      enddo

   end function
   !
   function ftlFindIfNotContainer(container, pred) result(it)
      type(CAT(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME))          , intent(in) :: container
      procedure(UnaryPredicate)                                             :: pred
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator))             :: it

      it = ftlFindIfNotIteratorPair(container%Begin(), container%End(), pred)

   end function



   ! Returns an iterator to the first element in the range [first1,last1) that matches any of the elements in [first2,last2).
   ! If no such element is found, the function returns last1.
   !
   ! The elements in [first1,last1) are sequentially compared to each of the values in [first2,last2) using operator== (or
   ! comp, if present), until a pair matches.
   !
   function ftlFirstOfIteratorPair(first1, last1, first2, last2, comp) result(firstOf)
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)), intent(in) :: first1, last1, first2, last2
      procedure(Comparator)                                    , optional   :: comp
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator))             :: firstOf

      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)) :: it

      if (first2 == last2) then
         firstOf = last1
         return
      endif

      firstOf = first1
      do while (firstOf /= last1)
         it = first2
         do while (it /= last2)
            if (present(comp)) then
               if (comp(firstOf%value, it%value)) return
            else
               if (firstOf%value == it%value) return
            endif
            call it%Inc()
         enddo
         call firstOf%Inc()
      enddo

   end function
   !
   function ftlFirstOfContainer(container1, container2, comp) result(firstOf)
      type(CAT(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME))          , intent(in) :: container1, container2
      procedure(Comparator)                                    , optional   :: comp
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator))             :: firstOf

      firstOf = ftlFirstOfIteratorPair(container1%Begin(), container1%End(), container2%Begin(), container2%End(), comp)

   end function


   ! Returns the number of elements in the range [first,last) that compare equal to val.
   !
   ! The function uses the == operator to compare the individual elements to val.
   !
   integer function ftlCountIteratorPair(first, last, val) result(num)
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)), intent(in) :: first, last
      FTL_TEMPLATE_TYPE                                        , intent(in) :: val

      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)) :: it

      num = 0
      it = first
      do while (it /= last)
         if (it%value == val) num = num + 1
         call it%Inc()
      enddo

   end function
   !
   integer function ftlCountContainer(container, val) result(num)
      type(CAT(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME)), intent(in) :: container
      FTL_TEMPLATE_TYPE                              , intent(in) :: val

      num = ftlCountIteratorPair(container%Begin(), container%End(), val)

   end function



   ! Returns the number of elements in the range [first,last) for which pred is true.
   !
   integer function ftlCountIfIteratorPair(first, last, pred) result(num)
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)), intent(in) :: first, last
      procedure(UnaryPredicate)                                             :: pred

      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)) :: it

      num = 0
      it = first
      do while (it /= last)
         if (pred(it%value)) num = num + 1
         call it%Inc()
      enddo

   end function
   !
   integer function ftlCountIfContainer(container, pred) result(num)
      type(CAT(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME)), intent(in) :: container
      procedure(UnaryPredicate)                                   :: pred

      num = ftlCountIfIteratorPair(container%Begin(), container%End(), pred)

   end function



   ! Compares the elements in the range [first1,last1) with those in the range beginning at first2, and returns the
   ! first element of both sequences that does not match.
   !
   ! The elements are compared using operator== or comp (if present).
   !
   ! The function returns a pair of iterators to the first element in each range that does not match.
   !
   function ftlMismatchIterators(first1, last1, first2, comp) result(it)
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)), intent(in) :: first1, last1, first2
      procedure(Comparator)                                    , optional   :: comp
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator))             :: it(2)

      it(1) = first1
      it(2) = first2
      do while (it(1) /= last1)
         if (present(comp)) then
            if (.not.comp(it(1)%value,it(2)%value)) exit
         else
            if (it(1)%value /= it(2)%value) exit
         endif
         call it(1)%Inc()
         call it(2)%Inc()
      enddo

   end function
   !
   function ftlMismatchContainers(container1, container2, comp) result(it)
      type(CAT(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME))          , intent(in) :: container1, container2
      procedure(Comparator)                                    , optional   :: comp
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator))             :: it(2)

      it = ftlMismatchIterators(container1%Begin(), container1%End(), container2%Begin(), comp)

   end function



   ! Compares the elements in the range [first1,last1) with those in the range beginning at first2, and returns true if all
   ! of the elements in both ranges match.
   !
   ! The elements are compared using operator== (or comp, if present).
   !
   logical function ftlEqualIterators(first1, last1, first2, comp) result(equal)
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)), intent(in) :: first1, last1, first2
      procedure(Comparator)                                    , optional   :: comp

      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)) :: it1, it2

      equal = .true.
      it1 = first1
      it2 = first2

      do while (it1 /= last1)
         if (present(comp)) then
            equal = comp(it1%value, it2%value)
         else
            equal = (it1%value == it2%value)
         endif
         if (.not.equal) return
         call it1%Inc()
         call it2%Inc()
      enddo

   end function
   !
   logical function ftlEqualContainers(container1, container2, comp) result(equal)
      type(CAT(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME)), intent(in) :: container1, container2
      procedure(Comparator)                          , optional   :: comp

      if (container1%Size() /= container2%Size()) then
         equal = .false.
      else
         equal = ftlEqualIterators(container1%Begin(), container1%End(), container2%Begin(), comp)
      endif

   end function



   ! Compares the elements in the range [first1,last1) with those in the range beginning at first2, and returns true if all of the
   ! elements in both ranges match, even in a different order.
   !
   ! The elements are compared using operator==.
   !
   logical function ftlIsPermutationIterators(first1, last1, first2) result(isPermutation)
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)), intent(in) :: first1, last1, first2

      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)) :: firstdiff(2), last2, it1
      integer :: n

      isPermutation = .true.

      ! check if sequences are identical or at least have a common beginning (which we would ignore later)
      firstdiff = ftlMismatchIterators(first1, last1, first2)
      if (firstdiff(1) == last1) return

      ! get an iterator to the end of range 2
      last2 = firstdiff(2)
      last2 = ftlAdvance(last2, ftlDistance(firstdiff(1), last1))

      it1 = firstdiff(1)
      do while (it1 /= last1)

         if (ftlFind(firstdiff(1), it1, it1%value) == it1) then ! this is the first time we see it1%value
            n = ftlCount(firstdiff(2), last2, it1%value)
            if (n == 0) then ! it1%value is not anywhere in range 2
               isPermutation = .false.
               return
            endif
            if (n /= ftlCount(firstdiff(1), last1, it1%value)) then ! count of it1%value is different in range 1 and range 2
               isPermutation = .false.
               return
            endif
         endif

         call it1%Inc()
      enddo

   end function
   !
   logical function ftlIsPermutationContainers(container1, container2) result(isPermutation)
      type(CAT(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME)), intent(in) :: container1, container2

      if (container1%Size() /= container2%Size()) then
         isPermutation = .false.
      else
         isPermutation = ftlIsPermutationIterators(container1%Begin(), container1%End(), container2%Begin())
      endif

   end function



   ! Searches the range [first1,last1) for the first occurrence of the sequence defined by [first2,last2), and returns an
   ! iterator to its first element, or last1 if no occurrences are found.
   !
   ! The elements in both ranges are compared sequentially using operator== (or comp, if present): A subsequence of
   ! [first1,last1) is considered a match only when this is true for _all_ the elements of [first2,last2).
   !
   ! This function returns the first of such occurrences. For an algorithm that returns the last instead, see ftlFindEnd.
   !
   function ftlSearchIterators(first1, last1, first2, last2, comp) result(itFirst)
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)), intent(in) :: first1, last1, first2, last2
      procedure(Comparator)                                    , optional   :: comp
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator))             :: itFirst

      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)) :: it1, it2

      itFirst = first1
      if (first2 == last2) return ! specified by C++11

      do while (itFirst /= last1)
         it1 = itFirst
         it2 = first2
         do while (.true.)
            if (present(comp)) then
               if (.not.comp(it1%value, it2%value)) exit
            else
               if (it1%value /= it2%value) exit
            endif
            call it1%Inc()
            call it2%Inc()
            if (it2 == last2) return ! complete sequence found
            if (it1 == last1) then ! at the end of [first1,last1) but [first2,last2) sequence not complete yet
               itFirst = last1
               return
            endif
         enddo
         call itFirst%Inc()
      enddo

   end function
   !
   function ftlSearchContainers(container1, container2, comp) result(itFirst)
      type(CAT(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME))          , intent(in) :: container1, container2
      procedure(Comparator)                                    , optional   :: comp
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator))             :: itFirst

      if (container1%Size() < container2%Size()) then
         itFirst = container1%End()
      else
         itFirst = ftlSearchIterators(container1%Begin(), container1%End(), container2%Begin(), container2%End(), comp)
      endif

   end function



   ! =============> Modifying sequence operations:



   subroutine ftlIterSwapPrivate(it1,it2)
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)) :: it1, it2

      FTL_TEMPLATE_TYPE :: tmp

      tmp = it1%value
      it1%value = it2%value
      it2%value = tmp

   end subroutine



   ! =============> Partitions:



   ! Rearranges the elements from the range [first,last), in such a way that all the elements for which pred returns true
   ! precede all those for which it returns false. The iterator returned points to the first element of the second group.
   !
   ! The relative ordering within each group is not necessarily the same as before the call. See stable_partition for a
   ! function with a similar behavior but with stable ordering within each group.
   !
   function ftlPartitionIterators(first, last, pred) result(pivot)
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)), intent(in) :: first, last
      procedure(UnaryPredicate)                                             :: pred
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator))             :: pivot

      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)) :: itRev

      ! TODO: implementation for forward iterators

      pivot = first
      itRev = last
      do while (pivot /= itRev)
         do while (pred(pivot%value))
            call pivot%Inc()
            if (pivot == itRev) return
         enddo
         do while (.true.)
            call itRev%Dec()
            if (pivot == itRev) return
            if (pred(itRev%value)) exit
         enddo
         call ftlIterSwap(pivot,itRev)
         call pivot%Inc()
      enddo

   end function
   !
   function ftlPartitionContainer(container, pred) result(pivot)
      type(CAT(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME)           , intent(in) :: container
      procedure(UnaryPredicate)                                             :: pred
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator))             :: pivot

      pivot = ftlPartitionIterators(container%Begin(), container%End(), pred)

   end function



   ! Returns true if all the elements in the range [first,last) for which pred returns true precede those for which it
   ! returns false.
   !
   ! If the range is empty, the function returns true.
   !
   logical function ftlIsPartitionedIterators(first, last, pred) result(isPartitioned)
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)), intent(in) :: first, last
      procedure(UnaryPredicate)                                             :: pred

      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)) :: it

      isPartitioned = .true.
      it = first
      do while (it /= last)
         if (pred(it%value)) then
            call it%Inc()
         else
            exit
         endif
      enddo
      do while (it /= last)
         if (pred(it%value)) then
            isPartitioned = .false.
            return
         endif
         call it%Inc()
      enddo

   end function
   !
   logical function ftlIsPartitionedContainer(container, pred) result(isPartitioned)
      type(CAT(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME), intent(in) :: container
      procedure(UnaryPredicate)                                  :: pred

      isPartitioned = ftlIsPartitionedIterators(container%Begin(), container%End(), pred)

   end function



   ! =============> Sorting operations:



   subroutine ftlSortContainer(container, comp)
      type(CAT(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME)), intent(inout) :: container
#ifdef FTL_TEMPLATE_TYPE_PROVIDES_COMPARATOR_SMALLER
      procedure(Comparator), optional :: comp
#else
      procedure(Comparator)           :: comp
#endif

      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)) :: i, j, smallestIt

      ! TODO: implement something faster than selection sort ;-) ...

      j = container%Begin()
      do while (j /= container%End())
         smallestIt = j
         i = j
         call i%Inc()
         do while (i /= container%End())
            if (ftlIterCompare(i, smallestIt, comp)) smallestIt = i
            call i%Inc()
         enddo
         call ftlIterSwap(j, smallestIt)
         call j%Inc()
      enddo

   end subroutine



   ! Returns true if the range [first,last) is sorted into ascending order.
   !
   ! The elements are compared using operator< (if available), or comp (if present).
   !
   logical function ftlIsSortedIterators(first, last, comp) result(isSorted)
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)), intent(in) :: first, last
#ifdef FTL_TEMPLATE_TYPE_PROVIDES_COMPARATOR_SMALLER
      procedure(Comparator)                                    , optional   :: comp
#else
      procedure(Comparator)                                                 :: comp
#endif

      isSorted = (ftlIsSortedUntilIterators(first, last, comp) == last)

   end function
   !
   logical function ftlIsSortedContainer(container, comp) result(isSorted)
      type(CAT(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME)), intent(in) :: container
#ifdef FTL_TEMPLATE_TYPE_PROVIDES_COMPARATOR_SMALLER
      procedure(Comparator)                          , optional   :: comp
#else
      procedure(Comparator)                                       :: comp
#endif

      isSorted = ftlIsSortedIterators(container%Begin(), container%End(), comp)

   end function



   ! Returns an iterator to the first element in the range [first,last) which does not follow an ascending order.
   !
   ! The range between first and the iterator returned is sorted.
   !
   ! If the entire range is sorted, the function returns last.
   !
   ! The elements are compared using operator< (if available), or comp (if present).
   !
   function ftlIsSortedUntilIterators(first, last, comp) result(firstUnsorted)
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)), intent(in) :: first, last
#ifdef FTL_TEMPLATE_TYPE_PROVIDES_COMPARATOR_SMALLER
      procedure(Comparator)                                    , optional   :: comp
#else
      procedure(Comparator)                                                 :: comp
#endif
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator))             :: firstUnsorted

      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)) :: lastSorted

      firstUnsorted = first
      if (first == last) return

      lastSorted = first
      call firstUnsorted%Inc()
      do while (firstUnsorted /= last)
         if (ftlIterCompare(firstUnsorted, lastSorted, comp)) return
         call firstUnsorted%Inc()
         call lastSorted%Inc()
      enddo

   end function
   !
   function ftlIsSortedUntilContainer(container, comp) result(firstUnsorted)
      type(CAT(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME)), intent(in) :: container
#ifdef FTL_TEMPLATE_TYPE_PROVIDES_COMPARATOR_SMALLER
      procedure(Comparator)                          , optional   :: comp
#else
      procedure(Comparator)                                       :: comp
#endif
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator))   :: firstUnsorted

      firstUnsorted = ftlIsSortedUntilIterators(container%Begin(), container%End(), comp)

   end function



   ! =============> Iterator operations:



   logical function ftlIterComparePrivate(it1, it2, comp) result(iterCompare)
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)), intent(in) :: it1, it2
#ifdef FTL_TEMPLATE_TYPE_PROVIDES_COMPARATOR_SMALLER
      procedure(Comparator)                                    , optional   :: comp

      if (present(comp)) then
         iterCompare = comp(it1%value, it2%value)
      else
         iterCompare = (it1%value < it2%value)
      endif
#else
      procedure(Comparator)                                                 :: comp

      iterCompare = comp(it1%value, it2%value)
#endif
   end function



   function ftlAdvancePrivate(itin, n) result(itout)
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)), intent(in) :: itin
      integer                                                  , intent(in) :: n
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator))             :: itout

#ifdef FTL_CONTAINER_PROVIDES_RANDOM_ACCESS_ITERATOR
      itout = itin + n
#else
      integer :: i

      itout = itin
      do i = 1, n
         call itout%Inc()
      enddo
#endif

   end function



   integer function ftlDistancePrivate(it1, it2) result(dist)
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)), intent(in) :: it1, it2

#ifdef FTL_CONTAINER_PROVIDES_RANDOM_ACCESS_ITERATOR
      dist = it2 - it1
#else
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)) :: it

      dist = 0
      it = it1
      do while (it /= it2)
         dist = dist + 1
         call it%Inc()
      enddo
#endif

   end function



#ifdef FTL_CONTAINER_PROVIDES_RANDOM_ACCESS_ITERATOR
   ! =============> Heap:



   ! Rearranges the elements in the range [first,last) in such a way that they form a heap.
   !
   ! A heap is a way to organize the elements of a range that allows for fast retrieval of the element with the highest
   ! value at any moment (with pop_heap), even repeatedly, while allowing for fast insertion of new elements (with
   ! push_heap).
   !
   ! The element with the highest value is always pointed by first. The order of the other elements depends on the
   ! particular implementation, but it is consistent throughout all heap-related functions of this header.
   !
   ! The elements are compared using operator< (if available), or comp (if present): The element with the
   ! highest value is an element for which this would return false when compared to every other element in the range.
   !
   ! The standard container adaptor priority_queue calls make_heap, push_heap and pop_heap automatically to maintain heap
   ! properties for a container.
   !
   ! The elements are compared using operator< (if available, this would produce a max-heap), or comp (if present).
   !
   subroutine ftlMakeHeapIterators(first, last, comp)
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)), intent(in) :: first, last
#ifdef FTL_TEMPLATE_TYPE_PROVIDES_COMPARATOR_SMALLER
      procedure(Comparator)                                    , optional   :: comp
#else
      procedure(Comparator)                                                 :: comp
#endif

      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)) :: it

      it = first + (last-first) / 2
      do while (.true.)
         call HeapDown(first, last, it, comp)
         if (it == first) exit
         call it%Dec()
      enddo

   end subroutine
   !
   subroutine ftlMakeHeapContainer(container, comp)
      type(CAT(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME)), intent(in) :: container
#ifdef FTL_TEMPLATE_TYPE_PROVIDES_COMPARATOR_SMALLER
      procedure(Comparator)                          , optional   :: comp
#else
      procedure(Comparator)                                       :: comp
#endif

      call ftlMakeHeapIterators(container%Begin(), container%End(), comp)

   end subroutine



   ! Given a heap in the range [first,last-1), this function extends the range considered a heap to [first,last) by placing
   ! the value in (last-1) into its corresponding location within it.
   !
   ! A range can be organized into a heap by calling make_heap. After that, its heap properties are preserved if elements are
   ! added and removed from it using push_heap and pop_heap, respectively.
   !
   ! The elements are compared using operator< (if available), or comp (if present).
   !
   subroutine ftlPushHeapIterators(first, last, comp)
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)), intent(in) :: first, last
#ifdef FTL_TEMPLATE_TYPE_PROVIDES_COMPARATOR_SMALLER
      procedure(Comparator)                                    , optional   :: comp
#else
      procedure(Comparator)                                                 :: comp
#endif

      call HeapUpDown(first, last, last-1, comp)

   end subroutine
   !
   subroutine ftlPushHeapContainer(container, comp)
      type(CAT(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME)), intent(in) :: container
#ifdef FTL_TEMPLATE_TYPE_PROVIDES_COMPARATOR_SMALLER
      procedure(Comparator)                          , optional   :: comp
#else
      procedure(Comparator)                                       :: comp
#endif

      call ftlPushHeapIterators(container%Begin(), container%End(), comp)

   end subroutine



   ! Rearranges the elements in the heap range [first,last) in such a way that the part considered a heap is shortened by
   ! one: The element with the highest value is moved to (last-1).
   !
   ! While the element with the highest value is moved from first to (last-1) (which now is out of the heap), the other
   ! elements are reorganized in such a way that the range [first,last-1) preserves the properties of a heap.
   !
   ! A range can be organized into a heap by calling make_heap. After that, its heap properties are preserved if elements are
   ! added and removed from it using push_heap and pop_heap, respectively.
   !
   ! The elements are compared using operator< (if available), or comp (if present).
   !
   subroutine ftlPopHeapIterators(first, last, comp)
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)), intent(in) :: first, last
#ifdef FTL_TEMPLATE_TYPE_PROVIDES_COMPARATOR_SMALLER
      procedure(Comparator)                                    , optional   :: comp
#else
      procedure(Comparator)                                                 :: comp
#endif

      call ftlIterSwap(first, last-1)
      call HeapDown(first, last-1, first, comp)

   end subroutine
   !
   subroutine ftlPopHeapContainer(container, comp)
      type(CAT(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME)), intent(in) :: container
#ifdef FTL_TEMPLATE_TYPE_PROVIDES_COMPARATOR_SMALLER
      procedure(Comparator)                          , optional   :: comp
#else
      procedure(Comparator)                                       :: comp
#endif

      call ftlPopHeapIterators(container%Begin(), container%End(), comp)

   end subroutine



   ! Internal heap helper methods
   !
   subroutine HeapDown(first, last, downIn, comp)
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)), intent(in) :: first, last, downIn
#ifdef FTL_TEMPLATE_TYPE_PROVIDES_COMPARATOR_SMALLER
      procedure(Comparator)                                    , optional   :: comp
#else
      procedure(Comparator)                                                 :: comp
#endif

      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)) :: down, leftChild, rightChild, largerChild

      down = downIn
      do while (.true.)

         leftChild = first + (down-first)*2 + 1
         if (leftChild >= last) exit ! node is all the way at the bottom, we are done
         largerChild = leftChild

         rightChild = leftChild + 1
         if (rightChild < last) then
            if (ftlIterCompare(leftChild, rightChild, comp)) largerChild = rightChild
         endif

         if (ftlIterCompare(down, largerChild, comp)) then
            call ftlIterSwap(down, largerChild)
            down = largerChild
         else
            exit
         endif

      enddo

   end subroutine
   !
   subroutine HeapUpDown(first, last, upIn, comp)
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)), intent(in) :: first, last, upIn
#ifdef FTL_TEMPLATE_TYPE_PROVIDES_COMPARATOR_SMALLER
      procedure(Comparator)                                    , optional   :: comp
#else
      procedure(Comparator)                                                 :: comp
#endif

      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)) :: up, parent

      up = UpIn
      do while (up /= first)
         parent = first + (up-first-1)/2
         if (ftlIterCompare(up, parent, comp)) exit
         call ftlIterSwap(up, parent)
         up = parent
      enddo
      call HeapDown(first, last, up, comp)

   end subroutine



   ! Returns true if the range [first,last) forms a heap, as if constructed with make_heap.
   !
   ! The elements are compared using operator< (if available), or comp (if present).
   !
   logical function ftlIsHeapIterators(first, last, comp) result(isHeap)
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)), intent(in) :: first, last
#ifdef FTL_TEMPLATE_TYPE_PROVIDES_COMPARATOR_SMALLER
      procedure(Comparator)                                    , optional   :: comp
#else
      procedure(Comparator)                                                 :: comp
#endif

      isHeap = (ftlIsHeapUntilIterators(first, last, comp) == last)

   end function
   !
   logical function ftlIsHeapContainer(container, comp) result(isHeap)
      type(CAT(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME)), intent(in) :: container
#ifdef FTL_TEMPLATE_TYPE_PROVIDES_COMPARATOR_SMALLER
      procedure(Comparator)                          , optional   :: comp
#else
      procedure(Comparator)                                       :: comp
#endif

      isHeap = ftlIsHeapIterators(container%Begin(), container%End(), comp)

   end function



   ! Returns an iterator to the first element in the range [first,last) which is not in a valid position if the range is
   ! considered a heap (as if constructed with make_heap).
   !
   ! The range between first and the iterator returned is a heap.
   !
   ! If the entire range is a valid heap, the function returns last.
   !
   ! The elements are compared using operator< (if available), or comp (if present).
   !
   function ftlIsHeapUntilIterators(first, last, comp) result(heapUntil)
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)), intent(in) :: first, last
#ifdef FTL_TEMPLATE_TYPE_PROVIDES_COMPARATOR_SMALLER
      procedure(Comparator)                                    , optional   :: comp
#else
      procedure(Comparator)                                                 :: comp
#endif
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator))             :: heapUntil

      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator)) :: parent

      heapUntil = first
      do while (heapUntil /= last)
         parent = first + (heapUntil-first-1)/2
         if (ftlIterCompare(parent, heapUntil, comp)) return
         call heapUntil%Inc()
      enddo

   end function
   !
   function ftlIsHeapUntilContainer(container, comp) result(heapUntil)
      type(CAT(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME)), intent(in) :: container
#ifdef FTL_TEMPLATE_TYPE_PROVIDES_COMPARATOR_SMALLER
      procedure(Comparator)                          , optional   :: comp
#else
      procedure(Comparator)                                       :: comp
#endif
      type(CAT3(FTL_CONTAINER,FTL_TEMPLATE_TYPE_NAME,Iterator))   :: heapUntil

      heapUntil = ftlIsHeapUntilIterators(container%Begin(), container%End(), comp)

   end function

#endif /*FTL_CONTAINER_PROVIDES_RANDOM_ACCESS_ITERATOR*/


end module
#endif
